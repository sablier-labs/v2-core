// SPDX-License-Identifier: UNLICENSED
pragma solidity >=0.8.22 <0.9.0;

import { Errors } from "src/core/libraries/Errors.sol";

import { Adminable_Unit_Shared_Test } from "../../../shared/Adminable.t.sol";

contract TransferAdmin_Unit_Concrete_Test is Adminable_Unit_Shared_Test {
    function test_RevertWhen_CallerIsNotAdmin() external {
        // Make Eve the caller in this test.
        resetPrank(users.eve);

        // Run the test.
        vm.expectRevert(abi.encodeWithSelector(Errors.CallerNotAdmin.selector, users.admin, users.eve));
        adminableMock.transferAdmin(users.eve);
    }

    modifier whenCallerIsAdmin() {
        _;
    }

    function test_WhenNewAdminSameAsCurrentAdmin() external whenCallerIsAdmin {
        // It should emit a {TransferAdmin} event.
        vm.expectEmit({ emitter: address(adminableMock) });
        emit TransferAdmin({ oldAdmin: users.admin, newAdmin: users.admin });

        // Transfer the admin.
        adminableMock.transferAdmin(users.admin);

        // It should keep the same admin.
        address actualAdmin = adminableMock.admin();
        address expectedAdmin = users.admin;
        assertEq(actualAdmin, expectedAdmin, "admin");
    }

    modifier whenNewAdminNotSameAsCurrentAdmin() {
        _;
    }

    function test_WhenNewAdminIsZeroAddress() external whenCallerIsAdmin whenNewAdminNotSameAsCurrentAdmin {
        // It should emit a {TransferAdmin}.
        vm.expectEmit({ emitter: address(adminableMock) });
        emit TransferAdmin({ oldAdmin: users.admin, newAdmin: address(0) });

        // Transfer the admin.
        adminableMock.transferAdmin(address(0));

        // It should set the admin to the zero address.
        address actualAdmin = adminableMock.admin();
        address expectedAdmin = address(0);
        assertEq(actualAdmin, expectedAdmin, "admin");
    }

    function test_WhenNewAdminIsNotZeroAddress() external whenCallerIsAdmin whenNewAdminNotSameAsCurrentAdmin {
        // It should emit a {TransferAdmin} event.
        vm.expectEmit({ emitter: address(adminableMock) });
        emit TransferAdmin({ oldAdmin: users.admin, newAdmin: users.alice });

        // Transfer the admin.
        adminableMock.transferAdmin(users.alice);

        // It should set the new admin.
        address actualAdmin = adminableMock.admin();
        address expectedAdmin = users.alice;
        assertEq(actualAdmin, expectedAdmin, "admin");
    }
}
