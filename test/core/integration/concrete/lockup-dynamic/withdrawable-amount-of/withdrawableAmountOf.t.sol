// SPDX-License-Identifier: UNLICENSED
pragma solidity >=0.8.22 <0.9.0;

import { WithdrawableAmountOf_Integration_Concrete_Test } from
    "./../../lockup/withdrawable-amount-of/withdrawableAmountOf.t.sol";
import { LockupDynamic_Integration_Shared_Test, Integration_Test } from "./../LockupDynamic.t.sol";

contract WithdrawableAmountOf_LockupDynamic_Integration_Concrete_Test is
    LockupDynamic_Integration_Shared_Test,
    WithdrawableAmountOf_Integration_Concrete_Test
{
    function setUp() public virtual override(LockupDynamic_Integration_Shared_Test, Integration_Test) {
        LockupDynamic_Integration_Shared_Test.setUp();
    }

    function test_GivenStartTimeInPresent() external givenSTREAMINGStatus(defaults.WARP_26_PERCENT()) {
        vm.warp({ newTimestamp: defaults.START_TIME() });
        uint128 actualWithdrawableAmount = lockupDynamic.withdrawableAmountOf(defaultStreamId);
        uint128 expectedWithdrawableAmount = 0;
        assertEq(actualWithdrawableAmount, expectedWithdrawableAmount, "withdrawableAmount");
    }

    function test_GivenNoPreviousWithdrawals()
        external
        givenSTREAMINGStatus(defaults.WARP_26_PERCENT())
        givenStartTimeInPast
    {
        // Simulate the passage of time.
        vm.warp({ newTimestamp: defaults.START_TIME() + defaults.CLIFF_DURATION() + 3750 seconds });

        // Run the test.
        uint128 actualWithdrawableAmount = lockupDynamic.withdrawableAmountOf(defaultStreamId);
        // The second term is 7,500*0.5^{0.5}
        uint128 expectedWithdrawableAmount = defaults.segments()[0].amount + 5303.30085889910643e18;
        assertEq(actualWithdrawableAmount, expectedWithdrawableAmount, "withdrawableAmount");
    }

    function test_GivenPreviousWithdrawal()
        external
        givenSTREAMINGStatus(defaults.WARP_26_PERCENT())
        givenStartTimeInPast
    {
        // Simulate the passage of time.
        vm.warp({ newTimestamp: defaults.START_TIME() + defaults.CLIFF_DURATION() + 3750 seconds });

        // Make the withdrawal.
        lockupDynamic.withdraw({ streamId: defaultStreamId, to: users.recipient, amount: defaults.WITHDRAW_AMOUNT() });

        // Run the test.
        uint128 actualWithdrawableAmount = lockupDynamic.withdrawableAmountOf(defaultStreamId);

        // The second term is 7,500*0.5^{0.5}
        uint128 expectedWithdrawableAmount =
            defaults.segments()[0].amount + 5303.30085889910643e18 - defaults.WITHDRAW_AMOUNT();
        assertEq(actualWithdrawableAmount, expectedWithdrawableAmount, "withdrawableAmount");
    }
}
